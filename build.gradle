buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
}
apply plugin: 'forge'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories{
    maven { url = "http://nexus.theyeticave.net/content/repositories/pub_releases" }
    maven { url = "http://repo.bukkit.org/content/groups/public/" }
}


version = "1.0.0"
group= "ru.redenergy"
archivesBaseName = "forge-vault"


minecraft {
    version = "1.7.10-10.13.4.1541-1.7.10"
    runDir = "eclipse"
}

dependencies {
    compile('net.milkbowl.vault:VaultAPI:1.4')
    compile('org.bukkit:bukkit:1.7.9-R0.2')
}

publishing {
    publications {
        MainPublication(MavenPublication) {
            from components.java
            groupId project.group
            artifactId 'forge-vault'
            version project.version + "-1.7.10"
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MainPublication']
    pkg {
        repo = 'maven'
        name = 'forge-vault'
        userOrg = user
        publicDownloadNumbers = true
        licenses = ["MIT"]
        vcsUrl = "https://github.com/FRedEnergy/forge-vault.git"
        //noinspection GroovyAssignabilityCheck
        version {
            name = "1.0.0-1.7.10"
            desc = "Vault API for Forge under 1.7.10 version"
            vcsTag = "1.0"
        }
    }
}

processResources{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    
    into('/'){
        println(findJar('VaultAPI-1.4').getClass())
        from zipTree(findJar('VaultAPI-1.4').getAsPath());
    }
}

def findJar(prefix){
    configurations.runtime.filter { it.name.startsWith(prefix) }
}

